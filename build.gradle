apply plugin: 'java'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.slf4j:slf4j-api:1.7.+'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

repositories {
    jcenter()
}

group = 'com.example'
archivesBaseName = 'fooable'
version = '1.0'

apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

task testsJar(type: Jar) {
    from sourceSets.test.allSource
    classifier 'tests'
}

publishing {
    publications {
        fooable(MavenPublication) {
            from components.java
            artifactId archivesBaseName

            pom {
                withXml {
                    def root = asNode()
                    def licenses = root.appendNode('licenses')
                    def license = licenses.appendNode('license')
                    license.appendNode('name', 'Apache License, Version 2.0')
                    license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                }
            }

            artifact sourcesJar

            artifact javadocJar

            artifact testsJar
        }
    }
}